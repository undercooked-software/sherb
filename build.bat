@ECHO OFF
SETLOCAL
::»»»»»»
SET NO_OUTPUT=^>nul 2^>nul
SET PROJECT_ALIAS=sherb

SET PREPROCESSOR=/DUNICODE
SET EXCEPTIONS=/GR- /EHa-
SET WARNINGS=/WX /Wall 

SET SUBSYSTEM=/SUBSYSTEM:CONSOLE /ENTRY:main

SET COMMON_COMPILER_FLAGS=%PREPROCESSOR% /nologo %EXCEPTIONS% %WARNINGS% /FC
SET COMMON_LINKER_FLAGS=/INCREMENTAL:NO /OPT:REF %SUBSYSTEM% /NODEFAULTLIB
SET RELEASE_COMPILER_FLAGS=/O2

SET INTERNAL_LIBS=kernel32.lib

SET EXTERNAL_LIB_PATHS=/LIBPATH:"libs\"
SET EXTERNAL_LIBS=msvcrt.lib

SET COMMAND=%~1
SET PROCEED=false
IF /i "%COMMAND%"=="release" (SET PROCEED=true)

SET ERR_MSG="The system cannot find the batch label specified - %COMMAND%"
IF /i "%COMMAND%"=="clean" ^
SET ERR_MSG="The batch label '%COMMAND%' is not available to be called directly by the user."

IF "%PROCEED%"=="true" GOTO :%COMMAND%

CALL :Print %ERR_MSG%
GOTO :EOF
::»»»»»»
ENDLOCAL

:Release
CL %COMMON_COMPILER_FLAGS% /O2 %EXTERNAL_INCLUDE_PATHS% /Z7 "%PROJECT_ALIAS%.c" %INTERNAL_LIBS% /link %COMMON_LINKER_FLAGS% %EXTERNAL_LIB_PATHS% %EXTERNAL_LIBS%
CALL :CLEAN
GOTO :EOF

:Clean
DEL /S /Q "%COMMAND%\*.lib" %NO_OUTPUT%
DEL /S /Q "%COMMAND%\*.exp" %NO_OUTPUT%
GOTO :EOF

:Print
@ECHO %~1
GOTO :EOF
